#!/usr/bin/perl

use strict;
use FindBin qw($Bin);

if ($> != 0) {
  print "You must be root to run this command.\n";
  exit(-1);
}

chdir $Bin or die "could not chdir $Bin\n";

# get excluded dirs
my @EXCLUDE_DIR;
{ my $fn = "exclude_dir.conf";
  if (! -f $fn) {
    print "creating default exclude_dir.conf\n";
    open my $fh, "> $fn" or die "could not write $fn; $!\n";
    print $fh 
"# enter directories to exclude from virus scan 
# wildcard like */dir/*/foo are supported
/boot
/sys
/proc
/run
/dev
/var/log";
  }
  open my $fh, "< $fn" or die "could not read $fn; $!\n";
  while (<$fh>) {
    next if /^\s*$/ || /^\s*\#/;
    chomp;
    push @EXCLUDE_DIR, $_;
  }
  push @EXCLUDE_DIR, $Bin;
}

# update clam db
my $rv = system("freshclam","--quiet");
if ($rv!=0) {
  print STDERR "freshclam error code: $rv\n";
  exit(-2);
}

# create list of files to be scanned
my $SCANLIST_FN = ".fastclamscan.lst";
{ my @cmd = ('find','/');

  # exclude directories
  push @cmd, '-type','d', '(';
  for (my $i=0; $i<=$#EXCLUDE_DIR; ++$i) {
    push @cmd, '-o' if $i > 0;
    push @cmd, "-path", $EXCLUDE_DIR[$i];
  }
  push @cmd, ')', '-prune', '-o', '-type', 'f';
  if (-f $SCANLIST_FN) {
    push @cmd, '-newer', $SCANLIST_FN;
  } else {
    push @cmd, '-cmin','60';
  }
  push @cmd, '-fprint', $SCANLIST_FN;

  my $rv = system(@cmd);
  if ($rv!=0) {
    print STDERR "ERROR: code: $rv when running: ".join(' ', @cmd)."\n";
    exit(-3);
  }
}

# run clamscan for file list
{ my $rv = system("clamscan", "--quiet", "--infected", "-f", $SCANLIST_FN, '-l', 'clamscan.log');
  if ($rv==1) {
    my $msg = "clamav says virus found; see $Bin/clamscan.log";
    print STDERR $msg;
    system("logger", $msg);
  } elsif ($rv != 0) {
    my $msg = "clamav error code: $rv; see $Bin/clamscan.log";
    print STDERR $msg;
    system("logger", $msg);
  }
}

exit(0);
